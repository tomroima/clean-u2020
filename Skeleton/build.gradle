// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    ext {
        // Manifest version information!
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0
        def versionBuild = 0 // bump for dogfood builds, public betas, etc.

        androidMinSdkVersion = 14
        androidTargetSdkVersion = 22

        compileSdkVersion = 22
        buildToolsVersion = '22.0.1'

        androidApplicationId = 'com.example.gior.skeleton'
        androidVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        androidVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
    }

    task wrapper(type: Wrapper) {
        description 'Creates the gradle wrapper.'
        gradleVersion '2.2.1'
    }
    task runAcceptanceTests(dependsOn: [':presentation:connectedAndroidTest']) {
        description 'Run application acceptance tests.'
    }

    task runUnitTests(dependsOn: [':domain:test', ':data-test:test']) {
        description 'Run unit tests for both domain and data layers.'
    }
}
